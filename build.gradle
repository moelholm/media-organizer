buildscript {
    ext {
        commonsLangVersion = '2.4'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.25.0"
	id 'org.springframework.boot' version '2.2.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
}

group 'com.moelholm'

sourceCompatibility = 1.8

compileJava.dependsOn spotlessCheck
compileTestJava.dependsOn spotlessCheck

repositories {
    mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

    compile "commons-lang:commons-lang:${commonsLangVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind"
}

test {
	useJUnitPlatform()
}

spotless {
  java {
    // aosp (~ 4 spaces per tab instead of the usual 2)
    googleJavaFormat('1.7').aosp()
  }
}

task triggerJob {
    doLast {
        if (System.getenv('web.apiKey') != null && System.getenv('web.url') != null) {
            print "Running media organizer"
            System.getenv("web.url")
                    .toURL()
                    .getText(requestProperties: [Authorization: System.getenv('web.apiKey')])
        } else {
            logger.error "Did not find apiKey and/or url in environment variables!"
        }
    }
}